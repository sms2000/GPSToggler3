import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'com.android.application'

android {
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
    }

    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    def versionPropsFile = file('version.properties')
    def code = 1

    if (versionPropsFile.canRead()) {
        Properties versionProps = new Properties()

        versionProps.load(new FileInputStream(versionPropsFile))
        List<String> runTasks = gradle.startParameter.getTaskNames()
        def value = 0
        for (String item : runTasks) {
            if (item.contains("compileDebugSources") || item.contains("compileReleaseSources")) {
                value = 1
            }

            print item
        }
        code = Integer.parseInt(versionProps['VERSION_CODE']).intValue() + value
        versionProps['VERSION_CODE'] = code.toString()
        versionProps.store(versionPropsFile.newWriter(), null)
    } else {
        throw new GradleException("Could not read version.properties!")
    }

    defaultConfig {
        applicationId "ogp.com.gpstoggler3"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode code
        versionName "1.4." + code
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    sourceSets.main.jni.srcDirs = [] // disable automatic ndk-build call, which ignore our Android.mk

    task ndkBuild(type: Exec) {
        workingDir file('jni')
        commandLine getNdkBuildCmd()
    }

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn ndkBuild
    }

    task cleanNative(type: Exec) {
        workingDir file('jni')
        commandLine getNdkBuildCmd(), 'clean'
    }

    clean.dependsOn cleanNative
}

allprojects {
    repositories {
        jcenter()
        maven {
            url "https://maven.google.com"
        }
    }


    dependencies {
        implementation fileTree(include: ['*.jar'], dir: 'libs')
        api 'com.android.support:appcompat-v7:28.0.0'
        api 'com.android.support.constraint:constraint-layout:1.1.3'
        api 'com.android.support:design:28.0.0'
        api 'com.google.firebase:firebase-appindexing:16.0.2'
        api 'com.android.support:support-v4:28.0.0'
        api 'com.android.support:support-vector-drawable:28.0.0'
        api 'com.google.android.gms:play-services-drive:16.0.0'
        api 'com.google.android.gms:play-services-auth:16.0.1'
        api 'com.google.api-client:google-api-client:1.25.0'
        api 'com.google.apis:google-api-services-tasks:v1-rev35-1.19.0' exclude module: 'httpclient'
        api 'com.google.api-client:google-api-client-android:1.19.0' exclude module: 'httpclient'
        api 'com.google.http-client:google-http-client-gson:1.19.0' exclude module: 'httpclient'
    }
}

def getNdkDir() {
    if (System.env.ANDROID_NDK_ROOT != null)
        return System.env.ANDROID_NDK_ROOT

    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    def ndkdir = properties.getProperty('ndk.dir', null)
    if (ndkdir == null)
        throw new GradleException("NDK location not found. Define location with ndk.dir in the local.properties file or with an ANDROID_NDK_ROOT environment variable.")

    return ndkdir
}

def getNdkBuildCmd() {
    def ndkbuild = getNdkDir() + "/ndk-build"

    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        ndkbuild += ".cmd"
    }

    return ndkbuild
}